plugins {
    id("org.jetbrains.kotlin.jvm") version "$kotlinVersion"
    id("org.jetbrains.kotlin.kapt") version "$kotlinVersion"
    id("org.jetbrains.kotlin.plugin.allopen") version "$kotlinVersion"
    id("com.github.johnrengelman.shadow") version "7.1.2"
    id("io.micronaut.application") version "3.7.2"
    id("com.google.protobuf") version "0.8.15"
}

version = "0.1"
group = "io.github.tobyhs.redisflagd"

dependencyLocking {
    lockAllConfigurations()
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-reactive:$kotlinCoroutinesVersion"

    implementation "io.micronaut:micronaut-jackson-databind:$micronautVersion"
    implementation "io.micronaut:micronaut-validation:$micronautVersion"
    implementation "io.micronaut.grpc:micronaut-grpc-runtime:3.5.0"
    implementation "io.micronaut.kotlin:micronaut-kotlin-runtime:3.2.2"
    implementation "io.micronaut.redis:micronaut-redis-lettuce:5.3.2"

    implementation "jakarta.annotation:jakarta.annotation-api:2.1.0"

    implementation "io.grpc:grpc-kotlin-stub:$grpcKotlinVersion"

    runtimeOnly "ch.qos.logback:logback-classic:1.2.11"

    runtimeOnly "com.fasterxml.jackson.module:jackson-module-kotlin:2.14.2"

    testImplementation "io.micronaut:micronaut-http-client:$micronautVersion"
}


application {
    mainClass.set("io.github.tobyhs.redisflagd.ApplicationKt")
}

java {
    sourceCompatibility = JavaVersion.toVersion("17")
}

tasks {
    compileKotlin {
        kotlinOptions {
            jvmTarget = "17"
        }
    }
    compileTestKotlin {
        kotlinOptions {
            jvmTarget = "17"
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs("build/generated/source/proto/main/grpc")
            srcDirs("build/generated/source/proto/main/grpckt")
            srcDirs("build/generated/source/proto/main/java")
        }
    }
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:3.21.12" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:1.51.1" }
        grpckt { artifact = "io.grpc:protoc-gen-grpc-kotlin:${grpcKotlinVersion}:jdk8@jar" }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
            grpckt {}
        }
    }
}

micronaut {
    testRuntime("kotest")
    processing {
        incremental(true)
        annotations("io.github.tobyhs.redisflagd.*")
    }
}
